# 郭劲豪 2200013146@stu.pku.edu.cn
# Execution begins at address 0 
	.pos 0
	irmovq stack, %rsp  	# Set up stack pointer
	call main		# Execute main program
	halt			# Terminate program 

# Sample Linked list
.align 8
ele1:
	.quad 0x00a
	.quad ele2
ele2:
	.quad 0x0b0
	.quad ele3
ele3:
	.quad 0xc00
	.quad 0

main: 
	irmovq ele1,%rdi
	call sum_list
	ret

# long sum_list(list_ptr ls)
# ls in %rdi
sum_list:
	xorq %rax,%rax   # val = 0
	rrmovq %rdi,%rbx # ready for setting CC
	andq %rbx,%rbx   # set CC
	jmp test         # Goto test
loop:
	mrmovq (%rdi),%r8         # Get *start
	addq %r8,%rax             # Add to sum
	mrmovq 0x8(%rdi),%rdi     # ls = ls -> next
	rrmovq %rdi,%rbx          # ready for setting CC
	andq %rbx,%rbx            # set CC
test: jne loop # Stop when 0
	ret # Return

# Stack starts here and grows to lower addresses
	.pos 0x200
stack:
